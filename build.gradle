import com.nkuppan.giphybrowser.buildsrc.Libs

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath Libs.androidGradlePlugin
        classpath Libs.Kotlin.gradlePlugin

        /**
         * Navigation component Gradle plugin to support navigation graph
         */
        classpath Libs.AndroidX.Navigation.safeArgsGradlePlugin

        /**
         * Google Hilt library Gradle plugin to support dependency injection
         */
        classpath Libs.Google.Hilt.androidGradlePlugin
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.5.31'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint(Libs.ktlint) {
        attributes {
            attribute(Bundling.BUNDLING_ATTRIBUTE, getObjects().named(Bundling, Bundling.EXTERNAL))
        }
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
}

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}